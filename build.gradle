plugins {
  id 'java'
  id "io.freefair.lombok" version "8.14.1"
  id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.10"
  id "com.gradleup.shadow" version "8.3.4"
}

repositories {
  maven {
    name = 'papermc'
    url = 'https://repo.papermc.io/repository/maven-public/'
  }
  mavenCentral() // Add Maven Central as a fallback repository
}

dependencies {
  // Velocity API
  compileOnly libs.velocity
  annotationProcessor libs.velocity

  implementation libs.mariadb
  implementation libs.hikari
  implementation libs.boostedyaml
}

group = 'net.lania'
version = '1.3.4'
description = 'Whitelist plugin for managing a Minecraft whitelist using MySQL'

def targetJavaVersion = 21
java {
  def javaVersion = JavaVersion.toVersion(targetJavaVersion)
  sourceCompatibility = javaVersion
  targetCompatibility = javaVersion
  if (JavaVersion.current() < javaVersion) {
    toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
  }
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'

  if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
    options.release.set(targetJavaVersion)
  }
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')
def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
  def props = [
    'id'         : "laniawhitelist",
    'name'       : project.name,
    'version'    : project.version,
    'link'       : "https://github.com/lania-smp/VelocityWhitelist",
    'description': project.description,
    'authors'    : "rathinosk,keelfy",
    'buildDate'  : java.time.LocalDate.now().format(java.time.format.DateTimeFormatter.ofPattern("yyyy-MM-dd"))
  ]
  task.inputs.properties props

  task.from templateSource
  task.into templateDest
  task.expand props
}

shadowJar {
  relocate 'org.mariadb.jdbc', 'net.lania.whitelist.libs.mariadb'
  duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })

idea.project.settings.taskTriggers.afterSync(generateTemplates)